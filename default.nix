/** 
  Creates a derivation that runs `nixdoc` on a given Nix file and writes the
  rendered Markdown to `$out/${outMdFile}`. Useful for producing README-style
  docs directly from Nix sources.

  # Arguments

  - [pkgs] An attrset from nixpkgs that provides `nixdoc`
  - [inNixFile] Path to the Nix file to document
  - [outMdFile] Markdown filename to write inside $out (default: "README.md")
  - [version] Version string for the generated derivation (default: "no-version")
  - [pname] Package name for the derivation (default: derived from inNixFile)
  - [description] Description to use as the header title (default: derived from inNixFile basename)
  - [category] Category to use in the generated docs (default: "docs")

  # Returns

  A derivation whose $out contains `${outMdFile}` generated by `nixdoc`.

  # Example:
  ```nix
  { pkgs, ... }:
  pkgs.callPackage ./default.nix {
    inNixFile = ./default.nix;
    outMdFile = "README.md";
    pname = "nix-gen-docs-docs";
  }
  ```
*/
{ pkgs
, inNixFile
, outMdFile ? "README.md"
, version ? "no-version"
, pname ? "nix-genned-docs-for-${builtins.baseNameOf (toString inNixFile)}"
, description ? null
, category ? "docs"
}:

let
  inBaseName = builtins.baseNameOf (toString inNixFile);
  docDescription = if description != null then description else "Documentation for ${inBaseName}";
in
  pkgs.stdenvNoCC.mkDerivation {
    inherit pname version;

    nativeBuildInputs = [ pkgs.nixdoc ];

    phases = [ "installPhase" ];

    installPhase = ''
      mkdir -p $out

      nixdoc \
        --file ${inNixFile} \
        --category ${category} \
        --description "${docDescription}" \
        > $out/${outMdFile}
    '';
  }



